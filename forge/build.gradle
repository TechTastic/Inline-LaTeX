plugins {
    id 'com.gradleup.shadow'
    id "me.modmuss50.mod-publish-plugin"
}

loom {
    forge {
        mixinConfig "inlinelatex.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-forge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    modImplementation("com.samsthenerd.inline:inline-forge:$minecraft_version-$inline_version")
    runtimeOnly("io.github.llamalad7:mixinextras-forge:0.4.1")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

publishMods {
    file = remapJar.archiveFile
    additionalFiles.from(sourcesJar.archiveFile)
    changelog = getLatestChangelog()
    type = STABLE
    modLoaders.add("forge")

    version = project.version
    displayName = "Inline: LaTeX - $project.version"

    curseforge {
        accessToken = System.getenv("CURSEFORGE_TOKEN") == null ? "" : System.getenv("CURSEFORGE_TOKEN")
        projectId = "1457746"
        minecraftVersions.add(minecraft_version)

        requires("architectury-api", "inline")
    }

    modrinth {
        accessToken = System.getenv("MODRINTH_TOKEN") == null ? "" : System.getenv("MODRINTH_TOKEN")
        projectId = "eHmR2ON3"
        minecraftVersions.add(minecraft_version)

        requires("architectury-api")
        requires("inline")
    }
}
